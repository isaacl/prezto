function prompt_jerome_precmd {
  if [[ $? -gt 0 ]]; then
    prompt_dollar='%F{red}$%f'
  else
    prompt_dollar='$'
  fi
  git-info
}

function prompt_jerome_dollar {
}

function prompt_jerome_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_jerome_precmd

  # Set git-info parameters.
  #zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:indexed' format '%i'
  zstyle ':prezto:module:git:info:unindexed' format '%I'
  zstyle ':prezto:module:git:info:commit' format '%F{yellow}%.7c%f'
  # Kind of useless position info.
  # zstyle ':prezto:module:git:info:position' format '%F{blue}%p%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{041}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '%F{158}(%f$(coalesce "%b" "%p" "%c")%C%D%s%F{158})%f'

  # Define prompts.
  PROMPT=' %~${(e)git_info[prompt]} $prompt_dollar '
  RPROMPT=''
}

prompt_jerome_setup "$@"
